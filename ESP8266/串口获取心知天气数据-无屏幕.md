> [!todo] ArduinoJson.h 必须为 6.x 版本
> ![[ASLant_Files/0c50576031d3a5efdad55952c08f3993_MD5.jpg]]
[](../ASLant_Files/0c50576031d3a5efdad55952c08f3993_MD5.jpg)en: 0c50576031d3a5efdad55952c08f3993_MD5.jpg]]
[](../ASLant_Files/0c50576031d3a5efdad55952c08f3993_MD5.jpg), something went wrong. Original message: PermissionError (13, '����ϵͳ�ϵ��豸û�з������á�', None, 31)
> ```
> 
> <font color="#00b050">解决办法，重新安装 CH 340 驱动</font>
> [CH340串口驱动⭐⭐⭐](https://aslant.top/Cloud/OneDrive/Other/Drivers/CH340.exe)

## 准备

> [!quote] 注册心知天气获取 API 私钥
> 1. [心知天气 - 高精度气象数据 - 天气数据API接口 - 行业气象解决方案 (seniverse.com)](https://www.seniverse.com/)

## 源代码

```c
#include <ArduinoJson.h>
#include <ESP8266WiFi.h>

const char* ssid = "科创社";
const char* password = "kcsyyds.";
const char* host = "api.seniverse.com";
const int httpPort = 80;

String reqUserKey = "xxxxxxxxxxxxx";  // 私钥 //SMCAYPJCo9DIBFiep
String reqLocation = "jinan";             // 城市
String reqUnit = "c";                     // 摄氏/华氏

void setup() {
  Serial.begin(9600);
  Serial.println("");
  connectWiFi();
}

void loop() {
  String reqRes = "/v3/weather/now.json?key=" + reqUserKey + +"&location=" + reqLocation + "&language=en&unit=" + reqUnit;
  httpRequest(reqRes);
  delay(3000);
}

// 向心知天气服务器服务器请求信息并对信息进行解析
void httpRequest(String reqRes) {
  WiFiClient client;

  // 建立http请求信息
  String httpRequest = String("GET ") + reqRes + " HTTP/1.1\r\n" + "Host: " + host + "\r\n" + "Connection: close\r\n\r\n";
  Serial.println("");
  Serial.print("Connecting to ");
  Serial.print(host);

  // 尝试连接服务器
  if (client.connect(host, 80)) {
    Serial.println(" Success!");

    // 向服务器发送http请求信息
    client.print(httpRequest);
    Serial.println("Sending request: ");
    Serial.println(httpRequest);

    // 获取并显示服务器响应状态行
    String status_response = client.readStringUntil('\n');
    Serial.print("status_response: ");
    Serial.println(status_response);

    // 使用find跳过HTTP响应头
    if (client.find("\r\n\r\n")) {
      Serial.println("Found Header End. Start Parsing.");
    }

    // 利用ArduinoJson库解析心知天气响应信息
    parseInfo(client);
  } else {
    Serial.println(" connection failed!");
  }
  //断开客户端与服务器连接工作
  client.stop();
}

// 连接WiFi
void connectWiFi() {
  WiFi.begin(ssid, password);      // 启动网络连接
  Serial.print("Connecting to ");  // 串口监视器输出网络连接信息
  Serial.print(ssid);
  Serial.println(" ...");  // 告知用户NodeMCU正在尝试WiFi连接

  int i = 0;                               // 这一段程序语句用于检查WiFi是否连接成功
  while (WiFi.status() != WL_CONNECTED) {  // WiFi.status()函数的返回值是由NodeMCU的WiFi连接状态所决定的。
    delay(1000);                           // 如果WiFi连接成功则返回值为WL_CONNECTED
    Serial.print(i++);
    Serial.print(' ');                        // 此处通过While循环让NodeMCU每隔一秒钟检查一次WiFi.status()函数返回值
  }                                           // 同时NodeMCU将通过串口监视器输出连接时长读秒。
                                              // 这个读秒是通过变量i每隔一秒自加1来实现的。
  Serial.println("");                         // WiFi连接成功后
  Serial.println("Connection established!");  // NodeMCU将通过串口监视器输出"连接成功"信息。
  Serial.print("IP address:    ");            // 同时还将输出NodeMCU的IP地址。这一功能是通过调用
  Serial.println(WiFi.localIP());             // WiFi.localIP()函数来实现的。该函数的返回值即NodeMCU的IP地址。
}

// 利用ArduinoJson库解析心知天气响应信息
void parseInfo(WiFiClient client) {
  const size_t capacity = JSON_ARRAY_SIZE(1) + JSON_OBJECT_SIZE(1) + 2 * JSON_OBJECT_SIZE(3) + JSON_OBJECT_SIZE(6) + 230;
  DynamicJsonDocument doc(capacity);

  deserializeJson(doc, client);

  JsonObject results_0 = doc["results"][0];

  JsonObject results_0_now = results_0["now"];
  const char* results_0_now_text = results_0_now["text"];                // "Sunny"
  const char* results_0_now_code = results_0_now["code"];                // "0"
  const char* results_0_now_temperature = results_0_now["temperature"];  // "32"

  const char* results_0_last_update = results_0["last_update"];  // "2020-06-02T14:40:00+08:00"

  // 通过串口监视器显示以上信息
  String results_0_now_text_str = results_0_now["text"].as<String>();
  int results_0_now_code_int = results_0_now["code"].as<int>();
  int results_0_now_temperature_int = results_0_now["temperature"].as<int>();

  String results_0_last_update_str = results_0["last_update"].as<String>();

  Serial.println(F("======Weahter Now======="));
  Serial.print(F("Weather Now: "));
  Serial.print(results_0_now_text_str);
  Serial.print(F(" "));
  Serial.println(results_0_now_code_int);
  Serial.print(F("Temperature: "));
  Serial.println(results_0_now_temperature_int);
  Serial.print(F("Last Update: "));
  Serial.println(results_0_last_update_str);
  Serial.println(F("========================"));
}
```

## 效果

> [!tip] 运行结果
>  
> 
>```sh
10:02:39.814 -> Connecting to 科创社 ...
10:02:40.779 -> 0 1 2 3 
10:02:44.546 -> Connection established!
10:02:44.546 -> IP address:    192.168.68.52
10:02:44.546 -> 
10:02:44.546 -> Connecting to api.seniverse.com Success!
10:02:44.612 -> Sending request: 
10:02:44.612 -> GET /v3/weather/now.json?key=SMCAYPJCo9DIBFiep&location=jinan&language=en&unit=c HTTP/1.1
10:02:44.711 -> Host: api.seniverse.com
10:02:44.745 -> Connection: close
10:02:44.778 -> 
10:02:44.778 -> 
10:02:44.778 -> status_response: HTTP/1.1 200 OK
>
10:02:44.812 -> Found Header End. Start Parsing.
10:02:44.878 -> ======Weahter Now=======
10:02:44.878 -> Weather Now: Sunny 0
10:02:44.878 -> Temperature: 26
10:02:44.912 -> Last Update: 2024-05-13T09:53:45+08:00
10:02:44.945 -> ========================
>```
